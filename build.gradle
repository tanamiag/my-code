plugins {
    id 'java'
    id 'org.springframework.boot' version '2.7.10'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/milestone' }
    maven { url 'https://repo.spring.io/snapshot' }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
    useJUnitPlatform()
}

// 하위 모든 프로젝트 공통 세팅
subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    group 'org.example'
    version '1.0-SNAPSHOT'

    sourceCompatibility = '11'
    targetCompatibility = '11'
    compileJava.options.encoding = 'UTF-8'

    repositories {
        mavenCentral()
    }

    // 하위 모듈에서 공통으로 사용하는 세팅 추가
    dependencies {
        // Spring Boot
        annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-web-services'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'

        // lombok
        annotationProcessor 'org.projectlombok:lombok'
        implementation  'org.projectlombok:lombok'

        // 기타
        implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'

        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
    }

    test {
        useJUnitPlatform()
    }
}

// 각 프로젝트에서만 사용하는 의존성 추가
project(':demo-core') {

    // core 에서는 bootjar로 패키징 할 필요가 없으므로 추가
    bootJar.enabled = false
    jar.enabled = true

    dependencies {
        // https://mvnrepository.com/artifact/org.projectlombok/lombok
        implementation 'org.reflections:reflections:0.9.11'
        implementation 'org.apache.commons:commons-collections4:4.4'
    }
}

project(':demo-client') {
    dependencies {
        // 컴파일시 core 모듈을 가져온다.
        implementation project(':demo-core')
        implementation project(':demo-server-endpoint')
        implementation 'org.springframework.boot:spring-boot-starter-aop'
        implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    }
}

project(':demo-server') {
    dependencies {
        implementation project(':demo-core')
        implementation project(':demo-server-endpoint')
    }
}

project(':demo-server-endpoint') {
    dependencies {
        implementation project(':demo-core')
    }
}